#!/usr/bin/env perl

use warnings;
use strict;
use File::Copy;
use FindBin qw($Bin $Script);

use lib "$Bin/../lib";
use Outspam qw (read_config);

my $conf = {
  'queue_dir' => '',
  'hold_queue_dir' => '',
  'access_lock_msg'=> 'ERROR:450: 4.3.2 Service temporarilly unavailable for suspicious user activity',
  'access_file' => '',
  'access_update_cmd' => '',
 };

my $config_file = "$Bin/../etc/outspam.conf";
read_config($config_file,$conf);

my @actions = qw ( USER_LOCK USER_UNLOCK );

my $action;
my $option="";
my $user;

($action, $option, $user) = argv();

#print "action = $action\n" if defined $action;
#print "options = $option\n" if defined $option;

access_file($user,$action);

my @user_msgs;
if ( $action eq 'USER_LOCK' && $option eq 'with-queue' ) {
    if ( select_msgs($conf->{'queue_dir'},$user,\@user_msgs) ) {
        mv_msgs($conf->{'queue_dir'},$conf->{'hold_queue_dir'},\@user_msgs);
    }    
}    
elsif ( $action eq 'USER_UNLOCK' ) {
    if ( select_msgs($conf->{'hold_queue_dir'},$user,\@user_msgs) ) {
        if ( $option eq 'with-queue' ) {
            mv_msgs($conf->{'hold_queue_dir'},$conf->{'queue_dir'},\@user_msgs);
        }
        else {
            rm_msgs($conf->{'hold_queue_dir'},\@user_msgs);
        }    
    }    
}    

sub access_file {
    (my $user, my $action) = @_;
    my $f;
    my $access_content="";
    my $user_found;
    if ( -f $conf->{'access_file'} ) {
       open ($f, '<', $conf->{'access_file'}) or die "Could not open \"$conf->{'access_file'}\": $!";
       while ( readline($f) ) {
           if ( /^From:$user\s/ ) { $user_found = 1; }    
           else { chomp; $access_content .= "$_\n"; }
       }    
       close $f;
    }
    if ( $action eq 'USER_LOCK' || $action eq 'USER_UNLOCK') {
       open ($f, '>', $conf->{'access_file'}) or die "Could not write to \"$conf->{'access_file'}\": $!";
       print $f $access_content if $access_content;
       print $f "From:$user $conf->{'access_lock_msg'}\n" if ( $action eq 'USER_LOCK' );
       close $f;
       system($conf->{'access_update_cmd'}) == 0 or die "\"$conf->{'access_update_cmd'}\" failed";
    }    
}    


sub mv_msgs {
   (my $src, my $dst, my $msgs) = @_;
   my $moved = 0;
   foreach my $fname (@$msgs) {
      move ("$src/qf$fname", "$dst/qf$fname") or warn "Could not move file \"$src/qf$fname\": $!";
      move ("$src/df$fname", "$dst/df$fname") or warn "Could not move file \"$src/df$fname\": $!";
      $moved++;
   }
   return $moved;   
}    

sub rm_msgs {
   (my $dir, my $msgs) = @_;
   foreach my $fname (@$msgs) {
      unlink ("$dir/qf$fname") or warn "Could not remove file \"$dir/qf$fname\": $!";
      unlink ("$dir/df$fname") or warn "Could not remove file \"$dir/qf$fname\": $!";
   }    
}    

sub select_msgs {
  (my $dir, my $user, my $user_msgs) = @_;
  opendir (my $dh, $dir) or die "Could not access directory \"$dir\"";
  my @msgs =  grep { /^qf.*/ && -f "$dir/$_" } readdir($dh);
  closedir $dh;
  my @user_msgs;
  foreach my $fname (@msgs) {
      open (my $f, '<', "$dir/$fname") or die "Could not open \"$dir/$fname\": $!";
         while ( readline($f) ) {
            if ( /^S<(.*)\@/ ) {
               push (@$user_msgs, substr($fname,2)) if ( $1 eq $user );
               last; 
            }
         }
      close($f);
  }
  return scalar(@$user_msgs);
}    


sub argv {
   foreach my $a (@ARGV) {
      $action = "USER_LOCK" if $a =~ /^--?l(?:ock)?$/; 
      $action = "help" if $a =~ /^--?h(?:elp)?$/; 
      if ( $a =~ /^--*u(q)?(?:nlock)?/ ) {
         $action = "USER_UNLOCK" if $a =~ /^--*u(q)?(?:nlock)?$/;
         $option = "with-queue" if $1;
      }
      $option = "with-queue" if $a =~ /^-q$|^--with-queue$/;
      $user = $a if $a =~ /^\w+$/;
   }
   help() unless defined($action) && defined($user);
   return ($action,$option,$user);
}


sub help {
    print "Locks and unlocks user outgoing mail\n";
    print "usage: $0 {-l|--lock|-u|--unlock} [-q|--with-queue] user\n";
    exit;
}    
